plugins {
    id 'java-library'
    id 'idea'
    id 'maven-publish'
    id 'signing'
}

compileJava {
    sourceCompatibility = 17
    targetCompatibility = 17
}

repositories {
    mavenCentral()
}

dependencies {
    api project(":conductor-client")
    api project(":orkes-client")
    api project(":sdk")
    api project(":conductor-client-spring")

    implementation 'org.springframework.boot:spring-boot-starter:3.3.0'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'Orkes Conductor Client/SDK Spring'
                description = 'Spring autoconfig for Orkes Conductor Client and SDK'
                url = 'https://github.com/conductor-oss/conductor.git'
                scm {
                    connection = 'scm:git:git://github.com/conductor-oss/conductor.git'
                    developerConnection = 'scm:git:ssh://github.com/conductor-oss/conductor.git'
                    url = 'https://github.com/conductor-oss/conductor.git'
                }
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        organization = 'Orkes'
                        organizationUrl = 'https://orkes.io'
                        name = 'Orkes Development Team'
                        email = 'developers@orkes.io'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            if (project.hasProperty("mavenCentral")) {
                println "Publishing to Sonatype Repository"
                url = "https://s01.oss.sonatype.org/${project.version.endsWith('-SNAPSHOT') ? "content/repositories/snapshots/" : "service/local/staging/deploy/maven2/"}"
                credentials {
                    username project.properties.username
                    password project.properties.password
                }
            } else {
                url = "s3://orkes-artifacts-repo/${project.version.endsWith('-SNAPSHOT') ? "snapshots" : "releases"}"
                authentication {
                    awsIm(AwsImAuthentication)
                }
            }
        }
    }
}

test {
    useJUnitPlatform()
}